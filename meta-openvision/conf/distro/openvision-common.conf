#@TYPE: Distribution
#@NAME: OpenVision
#@DESCRIPTION: Embedded Linux Distribution Configuration for STB's

require reporefs.conf
require revision.conf
require preferred-provider-version.conf
require openvision-testers.conf

DISTRO_FEATURES_BACKFILL_CONSIDERED += "pulseaudio gobject-introspection-data"

BB_HASH_IGNORE_MISMATCH = "1"

DISTRO_NAME = "openvision"
DISTRO_HOST ?= "${@os.uname()[1]}"
DISTRO_VERSION = "develop"
DISTRO_TYPE = "release"
BUILD_VERSION = "master"
DEVELOPER_BUILD_VERSION = "new"
VISIONGCCVERSION = "10"
VISIONVERSION = "10.1"

MACHINE_FEATURES += "${@bb.utils.contains("TARGET_ARCH", "mipsel", "middleflash", "", d)}"

LANGUAGECHECK = "${@bb.utils.contains("MACHINE_FEATURES", "middleflash", "multilanguage", "extralanguage", d)}"
VISIONLANGUAGE = "${@bb.utils.contains("MACHINE_FEATURES", "smallflash", "english", "${LANGUAGECHECK}", d)}"

LIBEPLAYERCHECK = "${@bb.utils.contains("MACHINE_FEATURES", "libeplayer", "-libeplayer", "-gstreamer", d)}"
LIBEPLAYER = "${@bb.utils.contains("TARGET_ARCH", "sh4", "${LIBEPLAYERCHECK}", "", d)}"

MAINTAINER ?= "Open Vision developers <forum.openvision.tech>"

MACHINE_KERNEL_PR_append = "openvision"

MACHINETYPE ?= "${MACHINE}"

STB_PLATFORM ?= "${MACHINE}"

BLINDSCAN_BINARY ?= "blindscan"

IMAGEDIR ??= "${MACHINE}"

IMGDEPLOYDIR ?= "${DEPLOY_DIR_IMAGE}"

IMAGEDIRRECOVERY ?= "${MACHINE}-recovery"

KV ?= "${KERNELVERSION}"

FORCE ?= "no"

UBI_VOLNAME ?= "rootfs"

RCTYPE ??= "0"
RCNAME ??= "${@bb.utils.contains("MACHINE_FEATURES", "sh4stb", "spark", "dmm1", d)}"

BUILDCFG_VARS += "VISIONVERSION VISIONREVISION VISIONLANGUAGE BOX_BRAND STB_PLATFORM SOC_FAMILY KERNELVERSION ${@bb.utils.contains("MACHINE_FEATURES", "resellerid", "RESELLERID" , "", d)} HAVE_MULTILIB HAVE_MIDDLEFLASH HAVE_SMALLFLASH ${@bb.utils.contains_any("MACHINE_FEATURES", "7segment 7seg textlcd bwlcd96 bwlcd128 bwlcd140 bwlcd255 colorlcd128 colorlcd220 colorlcd390 colorlcd400 colorlcd480 colorlcd720 colorlcd800", "DISPLAY_TYPE" , "", d)} RCTYPE RCNAME DEVELOPER_NAME DISTRO_FEED_URI"

INHERIT += "extrausers"
EXTRA_USERS_PARAMS = "usermod -P openvision root;"

PYTHONPATHVERSION = "python3.8"

IPKG_VARIANT = "opkg"

FEED_NAME ?= "${DISTRO_NAME}-${DISTRO_VERSION}"
DISTRO_FEED_URI ?= "http://${DISTRO_HOST}/feeds/${FEED_NAME}"
DISTRO_FEED_PREFIX ?= "${DISTRO_NAME}"

IMAGE_NAME = "${IMAGE_BASENAME}-${DISTRO_VERSION}-${VISIONLANGUAGE}${LIBEPLAYER}-${VISIONVERSION}-${VISIONREVISION}-${MACHINE}"
IMAGE_NAME[vardepsexclude] += "DATE"
IMAGE_LINK_NAME = ""
CACHE ?= "${TMPDIR}/cache/${LIBC}/${MACHINE}"
DEPLOY_DIR ?= "${TMPDIR}/deploy/${LIBC}"
DEPLOY_DIR_IMAGE ?= "${DEPLOY_DIR}/images/${MACHINE}"

LICENSE_FLAGS_WHITELIST = "commercial"
COMMERCIAL_AUDIO_PLUGINS ?= "gst-plugins-ugly-mad gst-plugins-ugly-mpegaudioparse"
COMMERCIAL_VIDEO_PLUGINS ?= "gst-plugins-ugly-mpeg2dec gst-plugins-ugly-mpegstream gst-plugins-bad-mpegvideoparse"

# OE optimization defaults to -O2 which makes for much larger binaries.
# Override here to use -Os instead, resulting in smaller images.
FULL_OPTIMIZATION = "-Os -Wno-error -pipe ${DEBUG_FLAGS}"
O2_OPT = "${@bb.utils.contains("MACHINE_FEATURES", "smallflash", "-Os -Wno-error -pipe ${DEBUG_FLAGS}", "-O2 -pipe ${DEBUG_FLAGS}", d)}"
O3_OPT = "${@bb.utils.contains("MACHINE_FEATURES", "smallflash", "-Os -Wno-error -pipe ${DEBUG_FLAGS}", "-O3 -pipe ${DEBUG_FLAGS}", d)}"
# build some core libs with better compiler optimization for better performance
FULL_OPTIMIZATION_pn-flac = "${O3_OPT}"
FULL_OPTIMIZATION_pn-jpeg = "${O3_OPT}"
FULL_OPTIMIZATION_pn-lame = "${O3_OPT}"
FULL_OPTIMIZATION_pn-liba52 = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libdca = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libgcrypt = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libmad = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libogg = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libpng = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libsamplerate = "${O3_OPT}"
FULL_OPTIMIZATION_pn-libvorbis = "${O3_OPT}"
FULL_OPTIMIZATION_pn-tremor = "${O3_OPT}"
FULL_OPTIMIZATION_pn-zlib = "${O3_OPT}"
FULL_OPTIMIZATION_pn-dhrystone = "-O3 -Ofast -DNDEBUG -DTIME"

DISTRO_FEATURES += "bluez5 alsa bluetooth directfb ext2 ipv4 ipv6 largefile wifi nfs zeroconf pam openvision10"

QA_LOGFILE = "${TMPDIR}/qa.log"

OEINCLUDELOGS ?= "yes"
KERNEL_CONSOLE ?= "ttyS0,115200n8"

BB_GENERATE_MIRROR_TARBALLS ??= "1"

NO32LIBS ??= "1"

# Missing checksums should raise an error
BB_STRICT_CHECKSUM = "0"

# Place output in "tmp" as before, not "tmp-glibc"
TCLIBCAPPEND = ""

# Don't grab feeds for lower level archs, e.g. mips32el should not
# attempt to fetch mipsel, and armv7 need not fetch armv5 etcetera.
DISTRO_FEED_ARCHS = "all ${TUNE_PKGARCH} ${MACHINE_ARCH} ${OEM_ARCH}"

# Use OpenSSL only
PACKAGECONFIG_pn-curl = "ipv6 ssl proxy zlib libssh2"
PACKAGECONFIG_pn-openldap = "openssl modules mdb ldap meta monitor null passwd shell proxycache dnssrv ipv6"
PACKAGECONFIG_pn-neon = "expat openssl libproxy webdav zlib"

HOSTTOOLS += "glib-gettextize java svn unzip git-lfs msgfmt zip"

VIRTUAL-RUNTIME_base-utils-hwclock = " "

FETCHCOMMAND_wget = "/usr/bin/env wget -T 10 -t 5 -nv --passive-ftp --no-check-certificate -P ${DL_DIR} '${URI}'"
